apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.cloudoki.demo3"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        // Enabling multidex support.
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/io.grpc.ManagedChannelProvider'
        exclude 'META-INF/services/io.grpc.ManagedChannelProvider'
        pickFirst 'META-INF/license.txt'
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0'
        }
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each {
            task ->
                task.builtins {
                    remove javanano
                    java {
                    }
                }
                task.plugins {
                    grpc {
                    }
                }
        }
    }
}

ext {
    supportLibraryVersion = '27.1.1'
    grpcVersion = '1.0.0'
}

dependencies {
    // Generic dependencies
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'

    // Support libraries
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"

    // gRPC
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation("io.grpc:grpc-protobuf:${grpcVersion}") {
        exclude module: 'jsr305'
    }
    implementation("io.grpc:grpc-stub:${grpcVersion}") {
        exclude module: 'jsr305'
    }
    implementation("io.grpc:grpc-auth:${grpcVersion}") {
        exclude module: 'jsr305'
    }
    implementation("io.grpc:grpc-okhttp:${grpcVersion}") {
        exclude module: 'jsr305'
    }
    // OAuth2 for Google API
    implementation('com.google.auth:google-auth-library-oauth2-http:0.3.0') {
        exclude module: 'jsr305'
        exclude module: 'httpclient'
    }
    // secure request so that sslv3 won't be used
    implementation 'info.guardianproject.netcipher:netcipher:1.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.16.0'

}
